apply plugin: 'java-library'
apply plugin: 'com.google.protobuf'

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.8.0"
    }

    plugins {
        create("ts") {
            path = "../node_modules/.bin/protoc-gen-ts_proto"
        }
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
            task.plugins {
                ts {}
            }
        }
    }
}

dependencies {
    implementation 'com.google.protobuf:protobuf-javalite:4.26.1'
}

sourceSets {
    main {
        java {
            srcDir 'build/generated/source/proto/main/java'
        }
        proto {
            srcDir '../../proto'
        }
    }
}


java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
tasks.withType(ProcessResources) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('copyts', Copy) {
    dependsOn(generateProto)
    from 'build/generated/source/proto/main/ts/'
    into '../../app/proto/'
}

assemble {
    dependsOn(copyts)
}
